import java.text.SimpleDateFormat
import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'idea'
}

group 'cn.enaium'
version '1.3.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

jar {
    manifest.attributes(
            'Manifest-Version': 1.0,
            'Main-Class': 'cn.enaium.joe.Main',
            "Implementation-Title": "${project.name}",
            "Implementation-Version": "${project.version}",
            "Implementation-Vendor": "${project.group}",
            "Implementation-Timestamp": new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date()),
            "Premain-Class": "cn.enaium.joe.Agent",
            "Agent-Class": "cn.enaium.joe.Agent",
            "Can-Redefine-Classes": true,
            "Can-Retransform-Classes": true
    )
}

new File(System.getProperty("user.dir"), "run").mkdir()

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.quiltmc.org/repository/release' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'

    implementation 'com.formdev:flatlaf:3.5.2'
    implementation 'com.formdev:flatlaf-extras:3.5.2'
    //noinspection GradlePackageUpdate
    implementation 'com.miglayout:miglayout-swing:11.4.2'
    implementation 'com.github.bobbylight:RSyntaxTextArea:3.5.2'
    implementation 'org.ow2.asm:asm-tree:9.7.1'
    implementation 'org.ow2.asm:asm-util:9.7.1'
    implementation 'org.ow2.asm:asm-commons:9.7.1'
    implementation 'org.benf:cfr:0.152'
    implementation 'com.github.mstrobel.procyon:procyon-decompiler:v0.6.0'
    implementation 'org.vineflower:vineflower:1.10.1'
    implementation 'org.javassist:javassist:3.30.2-GA'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.tinylog:tinylog:1.3.6'
    implementation 'com.github.FabricMC:mapping-io:597f0722d6'

    //compileOnly files(Jvm.current().getToolsJar())//Must use jdk8
}

test {
    useJUnitPlatform()
}

shadowJar {
    dependencies {
        include(dependency('com.formdev:.*'))
        include(dependency('com.miglayout:.*'))
        include(dependency('com.github.bobbylight:RSyntaxTextArea'))
        include(dependency('org.ow2.asm:.*'))
        include(dependency('org.benf:cfr'))
        include(dependency('com.github.mstrobel.procyon:procyon-decompiler'))
        include(dependency('org.javassist:javassist'))
        include(dependency('com.google.code.gson:gson'))
        include(dependency('org.tinylog:.*'))
        include(dependency('org.quiltmc:quiltflower'))
        include(dependency('com.github.FabricMC:mapping-io'))
    }
    archiveClassifier.set('')
}

build.dependsOn shadowJar